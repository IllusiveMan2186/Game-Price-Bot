plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'jacoco'
}

group = 'com.gpb'
version = "1.2.1"

java {
	sourceCompatibility = '17'
}

springBoot {
	mainClass = 'com.gpb.email.GpbEmailApplication' // Replace with your actual main class
}

def getEnvOrProperty(name) {
	project.findProperty(name) ?: System.getenv(name) ?: {
		throw new GradleException("$name not specified. Set '$name' property or $name environment variable.")
	}()
}

repositories {
	mavenCentral()
	maven {
		allowInsecureProtocol = true
		url = getEnvOrProperty("DEPENDENCY_REPO_URL")
		credentials {
			username = getEnvOrProperty("DEPENDENCY_REPO_USERNAME")
			password = getEnvOrProperty("DEPENDENCY_REPO_PASSWORD")
		}
	}
}

configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}
}

dependencies {
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.15.2'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.2'

	implementation 'com.gpb:common:1.2.1'

	compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	implementation 'org.springframework.boot:spring-boot-starter'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'

	implementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '3.1.1'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.5.0'
	testImplementation 'org.mockito:mockito-inline:5.2.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'

			limit {
				minimum = 0.70
			}

			excludes = [
					'**.configuration.*',
					'com.gpb.email.GpbEmailApplication'
			]
		}
	}
}

check.dependsOn jacocoTestCoverageVerification
